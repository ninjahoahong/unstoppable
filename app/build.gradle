import java.security.InvalidKeyException

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

// Extra properties
ext {
    //Using semantic versioning
    buildMajorVersion = 0
    buildMinorVersion = 0
    buildPatchVersion = 1
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "com.ninjahoahong.unstoppable"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "${buildMajorVersion}.${buildMinorVersion}.${buildPatchVersion}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField("long", "MAX_HTTP_CACHE_SIZE", "10 * 1024 * 1024") // 10 MB of HTTP cache
        buildConfigField("String", "API_BASE_URL", '"https://opentdb.com/"')

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['autovaluegson.defaultCollectionsToEmpty': 'true']
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    androidExtensions {
        experimental = true
    }

    signingConfigs {
        release {
            try {
                storeFile file("demo.keystore")
                storePassword KEYSTORE_PASSWORD
                keyAlias "demo"
                keyPassword KEY_PASSWORD
            }
            catch (ex) {
                throw new InvalidKeyException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("okhttp3.logging.HttpLoggingInterceptor.Level", "REST_ADAPTER_LOG_LEVEL", "okhttp3.logging.HttpLoggingInterceptor.Level.NONE")
        }
        debug {
            buildConfigField("okhttp3.logging.HttpLoggingInterceptor.Level", "REST_ADAPTER_LOG_LEVEL", "okhttp3.logging.HttpLoggingInterceptor.Level.BODY")
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/javamail.providers'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/rxjava.properties'
    }

    lintOptions {
        // https://github.com/square/okio/issues/58
        warning 'InvalidPackage'
    }

    testOptions {
        unitTests.all {
            // All the usual Gradle options.
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.71"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.google.dagger:dagger:2.16'
    implementation 'com.android.support:support-v4:28.0.0'
    kapt 'com.google.dagger:dagger-compiler:2.16'
    implementation 'com.github.Zhuinden:simple-stack:1.13.1'

    implementation('com.squareup.retrofit2:retrofit:2.4.0') {
        // exclude Retrofitâ€™s OkHttp peer-dependency module and define your own module import
        exclude module: 'okhttp'
    }
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.16'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'org.jetbrains.anko:anko-commons:0.10.5'
    implementation 'org.jetbrains.anko:anko-sdk15-listeners:0.10.5'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    implementation 'io.realm:android-adapters:3.0.0'
    implementation 'com.github.vivchar:ViewPagerIndicator:1.1.0'
}